/*
 *   Tura - Application generation solution
 *
 *   Copyright (C) 2008-2020 2182342 Ontario Inc ( arseniy.isakov@turasolutions.com ).
 *
 *
 *   This project includes software developed by Arseniy Isakov
 *   http://sourceforge.net/p/tura/wiki/Home/
 *   All rights reserved. This program and the accompanying materials
 *   are made available under the terms of the Eclipse Public License v2.0
 *   which accompanies this distribution, and is available at
 *   http://www.eclipse.org/legal/epl-v20.html
 */

operation type::TypeElement isInternal( ) { 
         var assosiations = type::Assosiation.allInstances()->select(t|t.target.uid = self.uid  and t.internal);
         if (assosiations.size() > 0){
            return true;
         }
         return false;
}
operation type::TypeReference isInternal( ) { 
         var assosiations = type::Assosiation.allInstances()->select(t|t.target.uid = self.uid  and t.internal);
         if (assosiations.size() > 0){
            return true;
         }
         return  self.typeRef.isInternal();
}



operation type::TypeElement mappingType2XML( recipe:Recipe, ingredient :Ingredient) { 
      self.mappingType2XML(recipe,ingredient,"ALL");
}

operation type::TypeElement mappingType2Java( recipe:Recipe, ingredient :Ingredient) { 
      self.mappingType2Java(recipe,ingredient,"ALL");
}

operation type::TypeElement mappingType2JavaScript( recipe:Recipe, ingredient :Ingredient) { 
      self.mappingType2JavaScript(recipe,ingredient,"ALL");
}


operation type::TypeElement unwrap( ) { 
      if (self.isTypeOf(type::TypeReference)){
        return  self.typeRef;
     }
     return self;
      
}


operation type::TypeElement mappingType2XML( recipe:Recipe, ingredient :Ingredient,maskType) { 
    self.~mapper=null;
    self.~class=null;
    self.~namespace=null;
    self.~fullname=null;

    var appmappers =   new Native('java.util.ArrayList');  
    var util = new Native('org.tura.metamodel.commons.Util');

    var tp=self;
    if (self.isTypeOf(type::TypeReference)){
        tp = self.typeRef;
    }
    
    appmappers.addAll(recipe.mappers);
    appmappers.addAll(ingredient.mappers);


    for( appmapper in appmappers ){
         var selfUID = self.uid;
         tp = self;
         if (self.isTypeOf(type::TypeReference)){
             selfUID = self.typeRef.uid;
             tp = self.typeRef;
         }
          var set = appmapper.mappers.select(r|r.isKindOf(XMLTypeMapper) and r.typeRef.uid= selfUID);
          if ( set.size() <> 0 ){
              var type =  util.mapType(set,ingredient,tp,maskType) ;
              if ( type <> null){
                 self.~mapper="Type2XML";
                 self.~class=type.typename;
                 self.~namespace= type.namespace;
                 self.~fullname=self.~namespace+"#"+self.~class;
              }  
          }    
     }

     if (self.~class = null and tp <> null and not self.isTypeOf(type::Primitive)){
	    for( appmapper in appmappers ){
	         var pkg =  self.eContainer();
	         var pkgUID = self.eContainer().uid;
	         if (self.isTypeOf(type::TypeReference)){
	             pkgUID = self.typeRef.eContainer().uid;
	             pkg =  self.typeRef.eContainer();
	         }
	         
	         var set = appmapper.mappers.select(r|r.isKindOf(mapper::XMLPackageMapper) and r.packageRef.uid= pkgUID);
             if ( set.size() <> 0 ){
              var pkgMp =  util.mapPackage(set,ingredient,pkg,maskType) ;
 
              if ( pkgMp <> null){
                 self.~mapper="Type2XML";
                 self.~class=util.mergeAndCapitalize(tp.name);
                 self.~namespace= pkgMp.namespace;
                 self.~fullname=self.~namespace+"#"+self.~class;
              }  
             }
	     }
     }

     if (self.~class = null and tp <> null){
/*  Cannot throw exception */     
         var container = "Primitives";
         if ( not tp.isTypeOf(type::Primitive)){
            container = tp.eContainer().name;
         } 

         util.traceIfDebug("XMLMapper","Unable to find XMLMapper for type \"" + container+"."+tp.name+"\" for ingredient \"" + ingredient.name +"\"");
         self.~mapper="Type2XML";
         self.~class="Unable to find XMLMapper for type \"" + container+"."+ tp.name +"\" for ingredient \"" + ingredient.name +"\"";
         self.~namespace= "Unable to find XMLMapper for type \""+ container+"."+ tp.name +"\" for ingredient \"" + ingredient.name +"\"";
         self.~fullname=self.~namespace+"#"+self.~class;
     }

}



operation type::TypeElement mappingType2Java( recipe:Recipe, ingredient :Ingredient,maskType) { 
     self.~mapper=null;
     self.~class=null;
     self.~package= null;
     self.~fullName=null;
     self.~artifactId=null;
     self.~groupId=null;
     self.~version= null;
    
    var appmappers =   new Native('java.util.ArrayList');  
    var util = new Native('org.tura.metamodel.commons.Util');

    var tp=self;
    if (self.isTypeOf(type::TypeReference)){
        tp = self.typeRef;
    }
    
    appmappers.addAll(recipe.mappers);
    appmappers.addAll(ingredient.mappers);


    for( appmapper in appmappers ){
         var selfUID = self.uid;
         tp = self;
         if (self.isTypeOf(type::TypeReference)){
             selfUID = self.typeRef.uid;
             tp = self.typeRef;
         }
          var set = appmapper.mappers.select(r|r.isKindOf(JavaMapper) and r.typeRef.uid= selfUID);
          if ( set.size() <> 0 ){
              var type =  util.mapType(set,ingredient,tp,maskType) ;
              if ( type <> null){
                 self.~mapper="Type2Java";
                 self.~class=type.mappedToClassName;
                 self.~package= type.mappedToPackageName;
                 if (type.mappedToPackageName <>'' and type.mappedToPackageName <> null)
                     self.~fullName=type.mappedToPackageName+"."+type.mappedToClassName;
                  else
                     self.~fullName=type.mappedToClassName;
                 self.~artifactId=type.artifactId;
                 self.~groupId=type.groupId;
                 self.~version= type.version;
                 if (type.libraryName <> null)
                     self.~library = type.libraryName;
                 else{
                    if ( type.artifactId<> null and type.version <> null )
                        self.~library=type.artifactId+"-"+type.version+"."+type.artifactType;
                    else
                        self.~library=null;
                 }    
              }  
          }    
     }
     if (self.~class = null and tp <> null and not self.isTypeOf(type::Primitive)){
	    for( appmapper in appmappers ){
	         var pkg =  self.eContainer();
	         var pkgUID = self.eContainer().uid;
	         tp = self;
	         if (self.isTypeOf(type::TypeReference)){
	             pkgUID = self.typeRef.eContainer().uid;
	             pkg =  self.typeRef.eContainer();
	             tp = self.typeRef;
	         }
	         
	         var set = appmapper.mappers.select(r|r.isKindOf(mapper::JavaPackageMapper) and r.packageRef.uid= pkgUID);
             if ( set.size() <> 0 ){
              var pkgMp =  util.mapPackage(set,ingredient,pkg,maskType) ;
 
              if ( pkgMp <> null){
                 self.~mapper="Type2Java";
                 self.~class=util.mergeAndCapitalize(tp.name);
                 self.~package= pkgMp.mappedToPackageName;
                 self.~fullName=pkgMp.mappedToPackageName+"."+self.~class;
                 self.~artifactId=pkgMp.artifactId;
                 self.~groupId=pkgMp.groupId;
                 self.~version= pkgMp.version;
                 if (pkgMp.libraryName <> null)
                     self.~library = pkgMp.libraryName;
                 else{
                    if ( pkgMp.artifactId<> null and pkgMp.version <> null )
                        self.~library=pkgMp.artifactId+"-"+pkgMp.version+"."+pkgMp.artifactType;
                    else
                        self.~library=null;
                 }    
              }  
             
             }
	     }
     }
     if (self.~class = null and tp <> null){
/*  Cannot throw exception */     
         var container = "Primitives";
         if ( not tp.isTypeOf(type::Primitive)){
            container = tp.eContainer().name;
         } 


         util.traceIfDebug("JavaMapper","Unable to find JavaMapper for type \"" + container+"."+tp.name+"\" for ingredient \"" + ingredient.name +"\"");
         self.~mapper="NA";
         self.~class="Unable to find JavaMapper for type \"" + container+"."+ tp.name +"\" for ingredient \"" + ingredient.name +"\"";
         self.~package= "Unable to find JavaMapper for type \""+ container+"."+ tp.name +"\" for ingredient \"" + ingredient.name +"\"";
         self.~fullName="Unable to find JavaMapper for type \"" + container+"."+ tp.name +"\" for ingredient \"" + ingredient.name +"\"";
         self.~artifactId="Unable to find JavaMapper for type \""+ container+"." + tp.name +"\" for ingredient \"" + ingredient.name +"\"";
         self.~groupId="Unable to find JavaMapper for type \"" + container+"."+ tp.name +"\" for ingredient \"" + ingredient.name +"\"";
         self.~version= "Unable to find JavaMapper for type \"" + container+"."+ tp.name +"\" for ingredient \"" + ingredient.name +"\"";
         
     }
} 


operation type::TypeElement mappingType2JavaScript( recipe:Recipe, ingredient :Ingredient,maskType) { 

    self.~mapper=null;
    self.~url=null;

    var appmappers =   new Native('java.util.ArrayList');  
    var util = new Native('org.tura.metamodel.commons.Util');
    
    appmappers.addAll(recipe.mappers);
    appmappers.addAll(ingredient.mappers);
    
    var tp=self;

    for( appmapper in appmappers ){
         var set = appmapper.mappers.select(r|r.isKindOf(JavaScriptMapper) and  r.typeRef.uid= self.uid);
          if ( set.size() <> 0 ){
             var type =  util.mapType(set,ingredient,tp,maskType) ;
             if ( type <> null){
                 if (type.artifactType.toString() == "jar"  ){
			             self.~mapper="Type2JavaScript";
			             self.~url=type.libraryUrl;
			             self.~artifactId=type.artifactId;
			             self.~groupId=type.groupId;
			             self.~version= type.version;
			             if ( type.artifactId<> null and type.version <> null ){
			                    self.~library=type.artifactId+"-"+type.version+"."+type.artifactType;
			             }else{
			                    self.~library=null;
			             }             
                 }         
                 if (type.artifactType.toString() == "jsPack"  ){
		             self.~mapper="Type2JavaScript";
		             self.~version= type.version;
		             self.~jsPackageName = type.jsPackageName;
		             self.~class = type.mappedToClassName;
		             self.~location = type.mappedToInternalLocation;
		             self.~url =type.libraryUrl;
		             if (self.~url == null ){
		                self.~url = "NA";
		             }
		             self.~library=type.jsPackageName+"-"+type.version;
		             self.~local=type.local;
			         if ( not self.~local){
			            self.~dependency = "\""+self.~jsPackageName+"\":\""+self.~version+"\"";
			         }else{
			            self.~dependency = "\""+self.~jsPackageName+"\":\""+self.~url+"\"";
			         }
             }         
           }
         }
    }
    
     if (self.~url = null ){
         self.~mapper="NA";
/*  Cannot throw exception */     
         util.traceIfDebug("Type2JavaScript","Unable to find Type2JavaScript for type \"" + self.name +"\" for ingredient \"" + ingredient.name +"\"");
     }    
}


operation type::TypeElement importJavaScript( recipe:Recipe, ingredient :Ingredient, component, relativePath ) { 
       self.mappingType2JavaScript(recipe,ingredient);
       if( component.packageName <> null and component.packageName.equals(  self.~jsPackageName)){
         self.~importString = relativePath+self.~location;
       }else{
         self.~importString = self.~jsPackageName+self.~location;
       }


}
